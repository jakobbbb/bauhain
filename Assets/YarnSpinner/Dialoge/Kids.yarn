title: IntroKids
position: -3653,-2709
---
<<set $backwards = 0>>
Ah, a... person?
Yeah, no, that's obviously three kids in a trenchcoat.
Why did you let them in?
At least they seem well behaved.
For now.
Quick!
<<jump BasicInteractionKids>>
===
title: BasicInteractionKids
position: -3421,-2741
---

-> Say something! <<if visited("SayKids") == false>>
    <<jump SayKids>>
-> Do something! <<if visited("DoKids") == false>>
    <<jump DoKids>>
-> Items? <<if visited("ItemsKids") == false>>
    <<jump ItemsKids>>
-> Leave. <<if visited("SayKids") && visited("DoKids") && visited("ItemsKids") == true>>
    <<set $kids = 2>>
    <<set $clock = $clock + 1>>
    <<jump EventLoop>>
===

title: SayKids
position: -3551,-2488
---
    <<if $backwards == 0>>
    -> say something in Ye Old English
        You: I do wish thou wert a dog, that I might love thee something ...
        Kids: Ok, rude.
    -> tell them about your childhood
        They do not care.
        At all.
    -> ask their opinion on an obscure animated film
        They were probably not born when that one came out.
    -> explain one of your fan theories
        You explain why it is obvious that Frodo does not know Legolas' name.
        Kids: That makes sense.
    -> interrogate them about there whereabouts on April 16th 2023
        Kids: Uh... work?
    <<else>>
    -> hsilgnE dlO eY ni gnihtemos yas
        You: ... gnihtemos eeht evol thgim I taht ,god a trew uoht hsiw od I
        Kids: Ok, rude.
    -> doohdlihc ruoy tuoba meht llet
        They do not care.
        At all.
    -> mlif detamina erucsbo na no noinipo rieht ksa
        They were probably not born when that one came out.
    -> seiroeht naf ruoy fo eno nialpxe
        You explain why it is obvious that Frodo does not know Legolas' name.
        Kids: That makes sense.
    -> 3202 ht61 lirpA no stuobaerehw ereht tuoba meht etagorretni
        Kids: Uh... work?
    <<endif>>
<<jump BasicInteractionKids>>
===

title: DoKids
position: -2970,-2635
---
    -> start speaking backwards
        <<set $backwards = 1>>
    -> scream
        Don't do that.
        You don't want to scare them.
    -> imitate a seal
        You take a deep breath and start belting.
        The kids break into applause.
    -> blankly stare at them
        Don't do that.
        That's weird.
    -> twerk furiously
        No, that's ... how does the youth say... out?
        What is in right now?
        Flossing?
        You try to floss.
        You fail spectacularly.
        Embarrassing.
        The kids look at you with pity in their eyes.
<<jump BasicInteractionKids>>
===

title: ItemsKids
position: -3006,-2211
---
    -> 10€ note <<if $drink == 0 && $note == 2>>
        Kids: Uh, thank you.
        Kids: This will come in handy for... taxes.
    -> 2€ <<if $drink >1 && $note ==1>>
        Kids: Uh, thank you.
        Kids: This will come in handy for... taxes.
    -> Banana <<if $banana == 1>>
        Kids: Uh, thank you.
        Kids: Good source of potassium.
        <<set $banana = 0>>
    -> Drink <<if $drink == 1>>
        No.
        You're not going to do that.
    -> Schirmchen <<if $drink == 2>>
        Kids: Uh, thank you.
        <<set $drink = 3>>
    -> Snail <<if $snail == 1>>
        Do not give animals to children.
    -> white powder <<if $koks == 1>>
        No.
        You're not going to do that.
    -> Your pockets are empty. <<if $note == 0 && $drink == 0 && $snail == 0 && $koks == 0>>
    -> Your pockets are empty. <<if $note == 0 && $drink == 3 && $snail == 0 && $koks == 0>>
<<jump BasicInteractionKids>>
===
