title: IntroNoah
position: -3440,-2597
---
<<set $backwards = 0>>
Ah, a person.
Quick!
<<jump BasicInteractionNoah>>
===
title: BasicInteractionNoah
position: -3211,-2474
---

-> Say something! <<if visited("SayNoah") == false>>
    <<jump SayNoah>>
-> Do something! <<if visited("DoNoah") == false>>
    <<jump DoNoah>>
-> Items? <<if visited("ItemsNoah") == false>>
    <<jump ItemsNoah>>
-> Leave. <<if visited("SayNoah") && visited("DoNoah") && visited("ItemsNoah") == true>>
     <<set $Noah = 2>>
    <<set $clock = $clock + 1>>
    <<if $Cops == 4 && $Mia == 2 && $Mattheo == 2 && $Emma == 2>>
        <<jump MURDERCops>>
    <<endif>>
    <<if $Cops < 4 && $murder == "no">>
        <<set $chanceMURDER = 0>>
        <<if $clock == 1>>
            <<set $chanceMURDER = random_range(1, 20)>> //5% 
            <<if $chanceMURDER == 1>>
                <<jump MURDERNoah>>
            <<endif>>
        <<elseif $clock == 2>>
            <<set $chanceMURDER = random_range(1, 10)>> //10%
            <<if $chanceMURDER == 1>>
                <<jump MURDERNoah>>
            <<endif>>
        <<elseif $clock == 3>>
            <<set $chanceMURDER = random_range(1, 100)>> //15%
            <<if $chanceMURDER <= 15>>
                <<jump MURDERNoah>>
            <<endif>>
        <<elseif $clock == 4>>
            <<set $chanceMURDER = random_range(1, 5)>> //20%
            <<if $chanceMURDER == 1>>
                <<jump MURDERNoah>>
            <<endif>>
        <<elseif $clock == 5>>
            <<set $chanceMURDER = random_range(1, 4)>> //25%
            <<if $chanceMURDER == 1>>
                <<jump MURDERNoah>>
            <<endif>>
        <<elseif $clock == 6>>
            <<set $chanceMURDER = random_range(1, 50)>> //30%
            <<if $chanceMURDER <= 15>>
                <<jump MURDERNoah>>
            <<endif>>
        <<elseif $clock == 7>>
            <<set $chanceMURDER = random_range(1, 100)>> //35%
            <<if $chanceMURDER <= 35>>
                <<jump MURDERNoah>>
            <<endif>>
        <<elseif $clock == 8>>
            <<set $chanceMURDER = random_range(1, 25)>> //40%
            <<if $chanceMURDER <= 10>>
                <<jump MURDERNoah>>
            <<endif>>
        <<elseif $clock == 9>>
            <<set $chanceMURDER = random_range(1, 100)>> //45%
            <<if $chanceMURDER <= 45>>
                <<jump MURDERNoah>>
            <<endif>>
        <<elseif $clock == 10>>
            <<set $chanceMURDER = random_range(1, 2)>>
            <<if $chanceMURDER == 1>>
                <<jump MURDERNoah>>
            <<endif>>
        <<endif>>
    <<endif>>
    <<jump EventLoop>>
===

title: SayNoah
position: -3358,-2263
---
    <<if $backwards == 0>>
    -> say something in Ye Old English
        You: I do wish thou wert a dog, that I might love thee something ...
        Noah: I love dogs.
    -> tell them about your childhood
        Noah: Poor you.
    -> ask their opinion on an obscure animated film
        Noah's eyeroll seem well practiced.
    -> explain one of your fan theories
        You explain why it is obvious that Frodo does not know Legolas' name.
        Noah: Is that really important?
    -> interrogate them about there whereabouts on April 16th 2023
        Noah seems to avoid eye contact with you.
    <<else>>
    -> hsilgnE dlO eY ni gnihtemos yas
        You: ... gnihtemos eeht evol thgim I taht ,god a trew uoht hsiw od I
        Noah: I love dogs.
    -> doohdlihc ruoy tuoba meht llet
        Noah: Poor you.
    -> mlif detamina erucsbo na no noinipo rieht ksa
        Noah's eyeroll seem well practiced.
    -> seiroeht naf ruoy fo eno nialpxe
        You explain why it is obvious that Frodo does not know Legolas' name.
        Noah: Is that really important?
    -> 3202 ht61 lirpA no stuobaerehw ereht tuoba meht etagorretni
        Noah seems to avoid eye contact with you.
    <<endif>>
<<jump BasicInteractionNoah>>
===

title: DoNoah
position: -2970,-2635
---
    -> start speaking backwards
        <<set $backwards = 1>>
    -> scream
        You: AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAH!
        Noah: Argh.
    -> imitate a seal
        You take a deep breath and start belting.
        Noah: Not bad.
    -> blankly stare at them
        ...
        Noah tries to poke your eyes.
        Rude.
    -> twerk furiously
        Noah: Wow.
        Noah: I'm impressed.
<<jump BasicInteractionNoah>>
===

title: ItemsNoah
position: -3006,-2211
---
    -> 10€ note <<if $drink == 0 && $note == 2>>
        Noah: Yeah, I needed that.
        <<set $note = 0>>
    -> 2€ <<if $drink >1 && $note ==1>>
        Noah: Yeah, I needed that.
        <<set $note = 0>>
    -> Banana <<if $banana == 1>>
        Noah: Yeah, I needed that.
        <<set $banana = 0>>
    -> Drink <<if $drink == 1>>
        Noah: Yeah, I needed that.
        <<set $drink = 2>>
    -> Schirmchen <<if $drink == 2>>
        Noah: Sweet.
        <<set $drink = 3>>
    -> Snail <<if $snail == 1>>
        Noah: Sweet.
        <<set $snail = 0>>
    -> white powder <<if $koks == 1>>
        <<if $Cops < 4 && $murder == "no">>
            <<jump MURDERNoah>>
        <<else>>
            Oof.
            Some people just can't handle their ...
            ... vitamins.
            Noahs mouth is opening and closing at an impressive speed
            but there's no sound coming out.
            Fortunately the worst will be over in a few minutes.
        <<endif>>
        <<set $koks = 0>>
    -> Your pockets are empty. <<if $note == 0 && $drink == 0 && $snail == 0 && $koks == 0>>
    -> Your pockets are empty. <<if $note == 0 && $drink == 3 && $snail == 0 && $koks == 0>>
<<jump BasicInteractionNoah>>
===
