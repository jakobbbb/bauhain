title: IntroEmma
position: -3440,-2597
---
<<set $backwards = 0>>
Ah, a person.
Quick!
<<jump BasicInteractionEmma>>
===
title: BasicInteractionEmma
position: -3211,-2474
---

-> Say something! <<if visited("SayEmma") == false>>
    <<jump SayEmma>>
-> Do something! <<if visited("DoEmma") == false>>
    <<jump DoEmma>>
-> Items? <<if visited("ItemsEmma") == false>>
    <<jump ItemsEmma>>
-> Leave. <<if visited("SayEmma") && visited("DoEmma") && visited("ItemsEmma") == true>>
    <<set $Emma = 2>>
    <<set $clock = $clock + 1>>
    <<if $Cops == 4 && $Mia == 2 && $Mattheo == 2 && $Noah == 2>>
        <<jump MURDERCops>>
    <<endif>>
    <<if $Cops < 4 && $murder == "no">>
        <<set $chanceMURDER = 0>>
        <<if $clock == 1>>
            <<set $chanceMURDER = random_range(1, 20)>> //5% 
            <<if $chanceMURDER == 1>>
                <<jump MURDEREmma>>
            <<endif>>
        <<elseif $clock == 2>>
            <<set $chanceMURDER = random_range(1, 10)>> //10%
            <<if $chanceMURDER == 1>>
                <<jump MURDEREmma>>
            <<endif>>
        <<elseif $clock == 3>>
            <<set $chanceMURDER = random_range(1, 100)>> //15%
            <<if $chanceMURDER <= 15>>
                <<jump MURDEREmma>>
            <<endif>>
        <<elseif $clock == 4>>
            <<set $chanceMURDER = random_range(1, 5)>> //20%
            <<if $chanceMURDER == 1>>
                <<jump MURDEREmma>>
            <<endif>>
        <<elseif $clock == 5>>
            <<set $chanceMURDER = random_range(1, 4)>> //25%
            <<if $chanceMURDER == 1>>
                <<jump MURDEREmma>>
            <<endif>>
        <<elseif $clock == 6>>
            <<set $chanceMURDER = random_range(1, 50)>> //30%
            <<if $chanceMURDER <= 15>>
                <<jump MURDEREmma>>
            <<endif>>
        <<elseif $clock == 7>>
            <<set $chanceMURDER = random_range(1, 100)>> //35%
            <<if $chanceMURDER <= 35>>
                <<jump MURDEREmma>>
            <<endif>>
        <<elseif $clock == 8>>
            <<set $chanceMURDER = random_range(1, 25)>> //40%
            <<if $chanceMURDER <= 10>>
                <<jump MURDEREmma>>
            <<endif>>
        <<elseif $clock == 9>>
            <<set $chanceMURDER = random_range(1, 100)>> //45%
            <<if $chanceMURDER <= 45>>
                <<jump MURDEREmma>>
            <<endif>>
        <<elseif $clock == 10>>
            <<set $chanceMURDER = random_range(1, 2)>>
            <<if $chanceMURDER == 1>>
                <<jump MURDEREmma>>
            <<endif>>
        <<endif>>
    <<endif>>
    <<jump EventLoop>>
===

title: SayEmma
position: -3358,-2263
---
    <<if $backwards == 0>>
    -> say something in Ye Old English
        You: Whether 'tis nobler in the mind to suffer, The slings and arrows of outrageous fortune, Or to take arms against a sea of troubles, And by opposing end them.
        Emma: Uh, maybe?
    -> tell them about your childhood
        You: My mother left when I was 3.
        Emma: Understandable.
    -> ask their opinion on an obscure animated film
        Emma: Never seen it.
        Emma: And also I don't care.
    -> explain one of your fan theories
        You launch into an indepth explanation of Larry Stylinson.
        Emma listens intensely.
        Emma: Those poor young men!       
    -> interrogate them about there whereabouts on April 16th 2023
        Emma side eyes you.
    <<else>>
    -> hsilgnE dlO eY ni gnihtemos yas
        You: .meht dne gnisoppo yb dnA ,selbuort fo aes a tsniaga smra ekat ot rO ,enutrof suoegartuo fo sworra dna sgnils ehT ,reffus ot dnim eht ni relbon sit' rehtehW
        Emma: Uh, maybe?
    -> doohdlihc ruoy tuoba meht llet
        You: .3 saw I nehw tfel rehtom yM
        Emma: Understandable.
    -> mlif detamina erucsbo na no noinipo rieht ksa
        Emma: Never seen it.
        Emma: And also I don't care.
    -> seiroeht naf ruoy fo eno nialpxe
        You launch into an indepth explanation of Larry Stylinson.
        Emma listens intensely.
        Emma: Those poor young men! 
    -> 3202 ht61 lirpA no stuobaerehw ereht tuoba meht etagorretni
        Emma side eyes you.
    <<endif>>
<<jump BasicInteractionEmma>>
===

title: DoEmma
position: -2970,-2635
---
    -> start speaking backwards
        <<set $backwards = 1>>
    -> scream
        You: AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAH!
        Emma: Hm.
        Emma: Take this.
        Emma hands you a snail.
        <<set $snail = 1>>
    -> imitate a seal
        You take a deep breath and start belting.
        Emma seems concerned.
    -> blankly stare at them
        ...
        Emma: Take this.
        Emma hands you a snail.
        <<set $snail = 1>>
    -> twerk furiously
        Emma: Incredible moves.
        Emma: Take this.
        Emma hands you a snail.
        <<set $snail = 1>>
<<jump BasicInteractionEmma>>
===

title: ItemsEmma
position: -3006,-2211
---
    -> 10€ note <<if $drink == 0 && $note == 2>>
        Emma: I cannot take this.
    -> 2€ <<if $drink >1 && $note ==1>>
        Emma: I cannot take this.
    -> Banana <<if $banana == 1>>
        <<if $Cops < 4 && $murder == "no">>
            <<jump MURDEREmma>>
        <<else>>
            You put your hand into your pocket and Emma flinches.
            You pull out the banana.
            Emma relaxes slighty
            but the hostile energy in the room stays off the charts.
            You decide to eat the banana yourself.
            Some people just don't appreciate a good source of potassium.
        <<endif>>
        <<set $banana = 0>>
    -> Drink <<if $drink == 1>>
        Emma: I cannot take this.
    -> Schirmchen <<if $drink == 2>>
        Emma: I cannot take this.
        Emma: ...
        Emma: Okay, maybe I can.
        Emma: Just this once.
        <<set $drink = 3>>
    -> Snail <<if $snail == 1>>
        Emma: Keep this.
    -> white powder <<if $koks == 1>>
        Emma: I cannot take this.
    -> Your pockets are empty. <<if $note == 0 && $drink == 0 && $snail == 0 && $koks == 0>>
    -> Your pockets are empty. <<if $note == 0 && $drink == 3 && $snail == 0 && $koks == 0>>
<<jump BasicInteractionEmma>>
===
