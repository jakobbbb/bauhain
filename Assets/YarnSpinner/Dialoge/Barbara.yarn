title: IntroBarbara
position: -3440,-2597
---
<<set $backwards = 0>>
Ah, a person.
Quick!
<<jump BasicInteractionBarbara>>
===
title: BasicInteractionBarbara
position: -3211,-2474
---

-> Say something! <<if visited("SayBarbara") == false>>
    <<jump SayBarbara>>
-> Do something! <<if visited("DoBarbara") == false>>
    <<jump DoBarbara>>
-> Items? <<if visited("ItemsBarbara") == false>>
    <<jump ItemsBarbara>>
-> Leave. <<if visited("SayBarbara") && visited("DoBarbara") && visited("ItemsBarbara") == true>>
    <<set $Barbara = 2>>
    <<set $clock = $clock +1>>
    <<if $Rentner == 4 && $Elfie == 2 && $Lizzie == 2 && $Fürchti == 2>>
        <<jump MURDERRentner>>
    <<endif>>
    <<if $Rentner < 4 && $murder == "no">>
        <<set $chanceMURDER = 0>>
        <<if $clock == 1>>
            <<set $chanceMURDER = random_range(1, 20)>> //5% 
            <<if $chanceMURDER == 1>>
                <<jump MURDERBarbara>>
            <<endif>>
        <<elseif $clock == 2>>
            <<set $chanceMURDER = random_range(1, 10)>> //10%
            <<if $chanceMURDER == 1>>
                <<jump MURDERBarbara>>
            <<endif>>
        <<elseif $clock == 3>>
            <<set $chanceMURDER = random_range(1, 100)>> //15%
            <<if $chanceMURDER <= 15>>
                <<jump MURDERBarbara>>
            <<endif>>
        <<elseif $clock == 4>>
            <<set $chanceMURDER = random_range(1, 5)>> //20%
            <<if $chanceMURDER == 1>>
                <<jump MURDERBarbara>>
            <<endif>>
        <<elseif $clock == 5>>
            <<set $chanceMURDER = random_range(1, 4)>> //25%
            <<if $chanceMURDER == 1>>
                <<jump MURDERBarbara>>
            <<endif>>
        <<elseif $clock == 6>>
            <<set $chanceMURDER = random_range(1, 50)>> //30%
            <<if $chanceMURDER <= 15>>
                <<jump MURDERBarbara>>
            <<endif>>
        <<elseif $clock == 7>>
            <<set $chanceMURDER = random_range(1, 100)>> //35%
            <<if $chanceMURDER <= 35>>
                <<jump MURDERBarbara>>
            <<endif>>
        <<elseif $clock == 8>>
            <<set $chanceMURDER = random_range(1, 25)>> //40%
            <<if $chanceMURDER <= 10>>
                <<jump MURDERBarbara>>
            <<endif>>
        <<elseif $clock == 9>>
            <<set $chanceMURDER = random_range(1, 100)>> //45%
            <<if $chanceMURDER <= 45>>
                <<jump MURDERBarbara>>
            <<endif>>
        <<elseif $clock == 10>>
            <<set $chanceMURDER = random_range(1, 2)>>
            <<if $chanceMURDER == 1>>
                <<jump MURDERBarbara>>
            <<endif>>
        <<endif>>
    <<endif>>
    <<dialogue_is_over>>
    <<jump EventLoop>>
===
title: SayBarbara
position: -3358,-2263
---
    <<if $backwards == 0>>
    -> say something in Ye Old English
        You: Shall I compare thee to a summer's day? Thou art more lovely and more temperate.
        Barbara: Okay.
    -> tell them about your childhood
        You: My mother left when I was 3.
        Barbara: Here, take this.
        You receive a small plastic bag with white powder.
        <<set $koks = 1>>
    -> ask their opinion on an obscure animated film
        Barbara: I do not watch movies.
    -> explain one of your fan theories
        You: ... and that is why Elvis is definitely alive!
        Barbara: Here, take this.
        You receive a small plastic bag with white powder.
        <<set $koks = 1>>
    -> interrogate them about there whereabouts on April 16th 2023
        Barbara: What are you? A cop?
    <<else>>
    -> hsilgnE dlO eY ni gnihtemos yas
        You: .etarepmet erom dna ylevol erom tra uohT ?yad s'remmus a ot eeht erapmoc I llahS
        Barbara: Okay.
    -> doohdlihc ruoy tuoba meht llet
        You: .3 saw I nehw tfel rehtom yM
        Barbara: Here, take this.
        You receive a small plastic bag with white powder.
        <<set $koks = 1>>
    -> mlif detamina erucsbo na no noinipo rieht ksa
    -> seiroeht naf ruoy fo eno nialpxe
        You: !evila yletinifed si sivlE yhw si taht dna ...
        Barbara: Here, take this.
        You receive a small plastic bag with white powder.
        <<set $koks = 1>>
    -> 3202 ht61 lirpA no stuobaerehw ereht tuoba meht etagorretni
        Barbara: What are you? A cop?
    <<endif>>
<<jump BasicInteractionBarbara>>
===

title: DoBarbara
position: -2970,-2635
---
    -> start speaking backwards
        <<set $backwards = 1>>
    -> scream
        You: AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAH!
        <<if $Rentner < 4 && $murder == "no">>
            <<jump MURDERBarbara>>
        <<else>>
            Barbara smileys at you broadly.
            You don't like it.
        <<endif>>
    -> imitate a seal
        You take a deep breath and start belting.
        Barbara starts singing which somehow sounds worse.
    -> blankly stare at them
        ...
        ...
        ...
        ...
        Does Barbara never blink??
        You lose the staring contest.
    -> twerk furiously
        Barbara joins in.
        Barbara definitely has more stamina than you.
<<jump BasicInteractionBarbara>>
===

title: ItemsBarbara
position: -3006,-2211
---
    -> 10€ note <<if $drink == 0 && $note == 2>>
        Barbara: Huh, thanks.
        Barbara rolls up the note and... uses it.
        <<set $note = 0>>
    -> 2€ <<if $drink >1 && $note ==1>>
        Barbara: Nah, thanks, I have no use for this.
    -> Banana <<if $banana == 1>>
        Barbara: Finally someone who actually has a banana in their pocket!
        <<set $banana = 0>>
    -> Drink <<if $drink == 1>>
        Barbara: Can't have too much of these!
        <<set $drink = 2>>
    -> Schirmchen <<if $drink == 2>>
        Barbara: Aww, cute.
        Barbara puts the Schirmchen in her hair.
        <<set $drink = 3>>
    -> Snail <<if $snail == 1>>
        Barbara: Love her but you can keep her.
    -> white powder <<if $koks == 1>>
        Barbara: I have more than enough of this.
<<jump BasicInteractionBarbara>>
===
