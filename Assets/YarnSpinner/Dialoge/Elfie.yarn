title: IntroElfie
position: -4157,-3160
---
<<set $backwards = 0>>
<<if $playerroom == 6>>
    <<jump MURDERElfie>>
<<else>>
Ah, a person.
Quick!
<<jump BasicInteractionElfie>>
<<endif>>
===
title: BasicInteractionElfie
position: -4011,-2877
---

-> Say something! <<if visited("SayElfie") == false>>
    <<jump SayElfie>>
-> Do something! <<if visited("DoElfie") == false>>
    <<jump DoElfie>>
-> Items? <<if visited("ItemsElfie") == false>>
    <<jump ItemsElfie>>
-> Leave. <<if visited("SayElfie") && visited("DoElfie") && visited("ItemsElfie") == true>>
    <<set $Elfie = 2>>
    <<set $clock = $clock + 1>>
    <<if $Rentner == 4 && $Barbara == 2 && $Lizzie == 2 && $Fürchti == 2>>
        <<jump MURDERRentner>>
    <<endif>>
    <<if $Rentner < 4 && $murder == "no">>
        <<set $chanceMURDER = 0>>
        <<if $clock == 1>>
            <<set $chanceMURDER = random_range(1, 20)>> //5% 
            <<if $chanceMURDER == 1>>
                <<jump MURDERElfie>>
            <<endif>>
        <<elseif $clock == 2>>
            <<set $chanceMURDER = random_range(1, 10)>> //10%
            <<if $chanceMURDER == 1>>
                <<jump MURDERElfie>>
            <<endif>>
        <<elseif $clock == 3>>
            <<set $chanceMURDER = random_range(1, 100)>> //15%
            <<if $chanceMURDER <= 15>>
                <<jump MURDERElfie>>
            <<endif>>
        <<elseif $clock == 4>>
            <<set $chanceMURDER = random_range(1, 5)>> //20%
            <<if $chanceMURDER == 1>>
                <<jump MURDERElfie>>
            <<endif>>
        <<elseif $clock == 5>>
            <<set $chanceMURDER = random_range(1, 4)>> //25%
            <<if $chanceMURDER == 1>>
                <<jump MURDERElfie>>
            <<endif>>
        <<elseif $clock == 6>>
            <<set $chanceMURDER = random_range(1, 50)>> //30%
            <<if $chanceMURDER <= 15>>
                <<jump MURDERElfie>>
            <<endif>>
        <<elseif $clock == 7>>
            <<set $chanceMURDER = random_range(1, 100)>> //35%
            <<if $chanceMURDER <= 35>>
                <<jump MURDERElfie>>
            <<endif>>
        <<elseif $clock == 8>>
            <<set $chanceMURDER = random_range(1, 25)>> //40%
            <<if $chanceMURDER <= 10>>
                <<jump MURDERElfie>>
            <<endif>>
        <<elseif $clock == 9>>
            <<set $chanceMURDER = random_range(1, 100)>> //45%
            <<if $chanceMURDER <= 45>>
                <<jump MURDERElfie>>
            <<endif>>
        <<elseif $clock == 10>>
            <<set $chanceMURDER = random_range(1, 2)>>
            <<if $chanceMURDER == 1>>
                <<jump MURDERElfie>>
            <<endif>>
        <<endif>>
    <<endif>>
    <<hide_canvas>>
    <<jump EventLoop>>
===

title: SayElfie
position: -4248,-2578
---
    <<if $backwards == 0>>
    -> say something in Ye Old English
        You: I do wish thou wert a dog, that I might love thee something ...
        Elfie: Ha, you think that's Old English?
        Elfie: I'll show you Old English, kid!
        Elfie starts speaking in an early West Saxon dialect.
        Impressive.
    -> tell them about your childhood
        Elfie: That sounds like a you problem.
    -> ask their opinion on an obscure animated film
        Elfie: Spare me this modern nonsense!
    -> explain one of your fan theories
        You explain why it is obvious that Frodo does not know Legolas' name.
        Elfie seems to aggree.
    -> interrogate them about there whereabouts on April 16th 2023
        <<if $Rentner < 4 && $murder == "no">>
            <<jump MURDERElfie>>
        <<else>>
            Elfie stares at you, unblinking.
            Seriously that's a lot of not blinking.
        <<endif>>
    <<else>>
    -> hsilgnE dlO eY ni gnihtemos yas
        You: ... gnihtemos eeht evol thgim I taht ,god a trew uoht hsiw od I
        Elfie: Ha, you think that's Old English?
        Elfie: I'll show you Old English, kid!
        Elfie starts speaking in an early West Saxon dialect.
        Impressive.
    -> doohdlihc ruoy tuoba meht llet
        Elfie: That sounds like a you problem.
    -> mlif detamina erucsbo na no noinipo rieht ksa
        Elfie: Spare me this modern nonsense!
    -> seiroeht naf ruoy fo eno nialpxe
        You explain why it is obvious that Frodo does not know Legolas' name.
        Elfie seems to aggree.
    -> 3202 ht61 lirpA no stuobaerehw ereht tuoba meht etagorretni
    <<if $Rentner < 4 && $murder == "no">>
            <<jump MURDERElfie>>
        <<else>>
            Elfie stares at you, unblinking.
            Seriously that's a lot of not blinking.
        <<endif>>
    <<endif>>
<<jump BasicInteractionElfie>>
===

title: DoElfie
position: -3466,-3171
---
    -> start speaking backwards
        <<set $backwards = 1>>
    -> scream
        You: AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAH!
        Elfie: What? I can't hear you.
        Elfie can definitely hear you.
    -> imitate a seal
        You take a deep breath and start belting.
        Elfie: I would toss you a fish if I had one.
    -> blankly stare at them
        ...
        Elfie: Are you trying to imtimidate me?
        You: I would never.
        You: because it would obviously not work.
    -> twerk furiously
        Elfie laughs in your face.
<<jump BasicInteractionElfie>>
===

title: ItemsElfie
position: -3694,-2583
---
    -> 10€ note <<if $drink == 0 && $note == 2>>
        Elfie: Nice.
        <<set $note = 0>>
    -> 2€ <<if $drink >1 && $note ==1>>
        Elfie: Eh, it's okay.
        <<set $note = 0>>
    -> Banana <<if $banana == 1>>
        Elfie: Love me a good source of potassium!
        <<set $banana = 0>>
    -> Drink <<if $drink == 1>>
        Elfie: Keep that, you need it more than me.
    -> Schirmchen <<if $drink == 2>>
        Elfie: Not today.
        <<set $drink = 3>>
    -> Snail <<if $snail == 1>>
        Elfie: I will put her on my head.
        <<set $snail = 0>>
    -> white powder <<if $koks == 1>>
        Elfie: Keep that, you need it more than me.
        <<set $koks = 0>>
    -> Your pockets are empty. <<if $note == 0 && $drink == 0 && $snail == 0 && $koks == 0>>
    -> Your pockets are empty. <<if $note == 0 && $drink == 3 && $snail == 0 && $koks == 0>>
<<jump BasicInteractionElfie>>
===
