title: IntroMattheo
position: -3440,-2597
---
<<set $backwards = 0>>
<<if $playerroom == 0>>
    <<jump MURDERMattheo>>
<<else>>
Ah, a person.
Quick!
<<jump BasicInteractionMattheo>>
<<endif>>
===
title: BasicInteractionMattheo
position: -3211,-2474
---

-> Say something! <<if visited("SayMattheo") == false>>
    <<jump SayMattheo>>
-> Do something! <<if visited("DoMattheo") == false>>
    <<jump DoMattheo>>
-> Items? <<if visited("ItemsMattheo") == false>>
    <<jump ItemsMattheo>>
-> Leave. <<if visited("SayMattheo") && visited("DoMattheo") && visited("ItemsMattheo") == true>>
    <<set $Mattheo = 2>>
    <<set $clock = $clock + 1>>
    <<if $Cops == 4 && $Mia == 2 && $Emma == 2 && $Noah == 2>>
        <<jump MURDERCops>>
    <<endif>>
    <<if $Cops < 4>>
        <<set $chanceMURDER = 0>>
        <<if $clock == 1>>
            <<set $chanceMURDER = random_range(1, 20)>> //5% 
            <<if $chanceMURDER == 1>>
                <<jump MURDERMattheo>>
            <<endif>>
        <<elseif $clock == 2>>
            <<set $chanceMURDER = random_range(1, 10)>> //10%
            <<if $chanceMURDER == 1>>
                <<jump MURDERMattheo>>
            <<endif>>
        <<elseif $clock == 3>>
            <<set $chanceMURDER = random_range(1, 100)>> //15%
            <<if $chanceMURDER <= 15>>
                <<jump MURDERMattheo>>
            <<endif>>
        <<elseif $clock == 4>>
            <<set $chanceMURDER = random_range(1, 5)>> //20%
            <<if $chanceMURDER == 1>>
                <<jump MURDERMattheo>>
            <<endif>>
        <<elseif $clock == 5>>
            <<set $chanceMURDER = random_range(1, 4)>> //25%
            <<if $chanceMURDER == 1>>
                <<jump MURDERMattheo>>
            <<endif>>
        <<elseif $clock == 6>>
            <<set $chanceMURDER = random_range(1, 50)>> //30%
            <<if $chanceMURDER <= 15>>
                <<jump MURDERMattheo>>
            <<endif>>
        <<elseif $clock == 7>>
            <<set $chanceMURDER = random_range(1, 100)>> //35%
            <<if $chanceMURDER <= 35>>
                <<jump MURDERMattheo>>
            <<endif>>
        <<elseif $clock == 8>>
            <<set $chanceMURDER = random_range(1, 25)>> //40%
            <<if $chanceMURDER <= 10>>
                <<jump MURDERMattheo>>
            <<endif>>
        <<elseif $clock == 9>>
            <<set $chanceMURDER = random_range(1, 100)>> //45%
            <<if $chanceMURDER <= 45>>
                <<jump MURDERMattheo>>
            <<endif>>
        <<elseif $clock == 10>>
            <<set $chanceMURDER = random_range(1, 2)>>
            <<if $chanceMURDER == 1>>
                <<jump MURDERMattheo>>
            <<endif>>
        <<endif>>
    <<endif>>
    <<jump EventLoop>>
===

title: SayMattheo
position: -3358,-2263
---
    <<if $backwards == 0>>
    -> say something in Ye Old English
        You: Shall I compare thee to a summer's day? Thou art more lovely and more temperate.
        Mattheo: If you want to...
    -> tell them about your childhood
        Mattheo: That's rough, buddy.
    -> ask their opinion on an obscure animated film
        Mattheo: Can you bother someone else with this?
    -> explain one of your fan theories
        Mattheo does not seem interested in your 15 minute monologue about why Sokka is most definitely an earthbender.
    -> interrogate them about there whereabouts on April 16th 2023
        Mattheo: Like I would tell you.
    <<else>>
    -> hsilgnE dlO eY ni gnihtemos yas
        You: .etarepmet erom dna ylevol erom tra uohT ?yad s'remmus a ot eeht erapmoc I llahS
        Mattheo: If you want to...
    -> doohdlihc ruoy tuoba meht llet
        Mattheo: That's rough, buddy.
    -> mlif detamina erucsbo na no noinipo rieht ksa
        Mattheo: Can you bother someone else with this?
    -> seiroeht naf ruoy fo eno nialpxe
        Mattheo does not seem interested in your 15 minute monologue about why Sokka is most definitely an earthbender.
    -> 3202 ht61 lirpA no stuobaerehw ereht tuoba meht etagorretni
        Mattheo: Like I would tell you.
    <<endif>>
<<jump BasicInteractionMattheo>>
===

title: DoMattheo
position: -2970,-2635
---
    -> start speaking backwards
        <<set $backwards = 1>>
    -> scream
        You: AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAH!
        Mattheo: Sometimes I feel screaming is the only sensible way to interact with this cruel world.
    -> imitate a seal
        You take a deep breath and start belting.
        Mattheo: You know what... I felt that.
    -> blankly stare at them
        ...
        Mattheo stare back at you with eyes like limpid tears.
        What does that remind you of...?
    -> twerk furiously
        Mattheo: Don't you like... work here?
        You: You make it sound like it's a bad thing.
        Mattheo squints, eyes following you bouncing behind.
        Mattheo: Appalling workplace behaviour.
        You: You are not the boss of me!
        <<if $Cops < 4>>
            Mattheo: Actually I am.
            <<jump MURDERMattheo>>
        <<else>>
            Mattheo: ...
            Mattheo: Unfortunately.
            You are not sure what that is supposed to mean.
        <<endif>>
<<jump BasicInteractionMattheo>>
===

title: ItemsMattheo
position: -3006,-2211
---
    -> 10€ note <<if $drink == 0 && $note == 2>>
        Mattheo: Thanks but that does not make me like you.
        <<set $note = 0>>
    -> 2€ <<if $drink >1 && $note ==1>>
        Mattheo: Thanks but that does not make me like you.
        <<set $note = 0>>
    -> Banana <<if $banana == 1>>
        Mattheo: Thanks but that does not make me like you.
        <<set $banana = 0>>
    -> Drink <<if $drink == 1>>
        Mattheo: Thanks but that does not make me like you.
        <<set $drink = 2>>
    -> Schirmchen <<if $drink == 2>>
        Mattheo: Thanks but that does not make me like you.
        <<set $drink = 3>>
    -> Snail <<if $snail == 1>>
        Mattheo: Thanks but that does not make me like you.
        <<set $snail = 0>>
    -> white powder <<if $koks == 1>>
        Mattheo: Thanks but that does not make me like you.
        <<set $koks = 0>>
    -> Your pockets are empty. <<if $note == 0 && $drink == 0 && $snail == 0 && $koks == 0>>
    -> Your pockets are empty. <<if $note == 0 && $drink == 3 && $snail == 0 && $koks == 0>>
<<jump BasicInteractionMattheo>>
===
